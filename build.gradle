/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

plugins {
    id "ca.coglinc.javacc" version "2.4.0"
}

apply plugin: "java"

version = "3.0.0-nightly"

repositories {
    // mavenLocal()
    mavenCentral()
}

configurations.all {
    // We use SLF4J with Logback binding, so exclude any other SLF4J bindings:
    exclude group: "org.slf4j", module: "slf4j-log4j12"
    exclude group: "org.slf4j", module: "slf4j-jdk14"

    // We use xxx-over-slf4j to substitute logging libraries, so exclude them:
    exclude group: "log4j", module: "log4j"
    exclude group: "commons-logging", module: "commons-logging"

    // xml-apis is part of the Java SE version for a good while; prevent old libraries pulling it in:
    exclude group: "xml-apis", module: "xml-apis"
}

configurations.testCompile {
    // Jetty pulls in its own version of Servlet/JSP classes, so don't inherit these from the "compile" configuration:
    exclude group: "javax.servlet.jsp", module: "jsp-api"
    exclude group: "javax.servlet.jsp", module: "servlet-api"
}

dependencies {
    def jettyVersion = "7.6.16.v20140903"
    def slf4jVersion = "1.7.22"
    def springVersion = "2.5.6.SEC03"

    compile "com.google.guava:guava:20.0"

    compile "jaxen:jaxen:1.0-FCS"
    compile "saxpath:saxpath:1.0-FCS"
    compile "xalan:xalan:2.7.0"

    compile "org.slf4j:slf4j-api:$slf4jVersion"

    compile "org.zeroturnaround:javarebel-sdk:1.2.2"

    // TODO @SuppressFBWarnings-s should be removed before build, then this dependency is only needed for the IDE
    compile "com.google.code.findbugs:annotations:3.0.0"

    // TODO These will be moved to the freemarker-serlvet module:
    compile "javax.servlet.jsp:jsp-api:2.1"
    compile "javax.servlet:servlet-api:2.5"

    // Test:

    testCompile "junit:junit:4.12"
    testCompile "org.hamcrest:hamcrest-library:1.3"

    testCompile "ch.qos.logback:logback-classic:1.1.8"
    testCompile "org.slf4j:jcl-over-slf4j:$slf4jVersion"

    testCompile "commons-io:commons-io:2.2"
    testCompile "com.google.guava:guava-jdk5:17.0"

    testCompile "org.eclipse.jetty:jetty-server:$jettyVersion"
    testCompile "org.eclipse.jetty:jetty-webapp:$jettyVersion"
    testCompile "org.eclipse.jetty:jetty-jsp:$jettyVersion"
    testCompile "org.eclipse.jetty:jetty-util:$jettyVersion"

    testCompile("displaytag:displaytag:1.2") {
        exclude group: "com.lowagie", module: "itext"
    }

    testCompile "org.springframework:spring-core:$springVersion"
    testCompile "org.springframework:spring-test:$springVersion"
}

compileJava {
    // TODO This will be 1.7 when freemarker-core-java8 is separated
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"

    options.encoding = "UTF-8"
}

compileTestJava {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"

    options.encoding = "UTF-8"
}

compileJavacc {
    arguments = [ grammar_encoding: "UTF-8" ]
    doLast {
        // TODO Some filtering is needed on the output - see in the original Ant build

        // Note: The Gradle JavaCC plugin automatically removes generated java files that are already in
        // src/main/java, so we don't need to get rid of ParseException.java and TokenMgrError.java (unlike in Ant)
    }
}

jar {
    // TODO Use bnd - see in the Ant build
    manifest {
        attributes(
                // TODO There were more here in the Ant build
                "Implementation-Title": "Apache FreeMarker",
                "Implementation-Version": project.version)
    }
}
